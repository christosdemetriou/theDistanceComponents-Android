apply plugin: 'com.android.application'

class Utils {
    static def r = new Random(System.currentTimeMillis())
}

def String toJavaCodeString(String string) {
    byte[] b = string.getBytes();
    int c = b.length;
    StringBuilder sb = new StringBuilder();

    sb.append("(new Object() {");
    sb.append("int t;");
    sb.append("public String toString() {");
    sb.append("byte[] buf = new byte[");
    sb.append(c);
    sb.append("];");

    for (int i = 0; i < c; ++i) {
        int t = Utils.r.nextInt();
        int f = Utils.r.nextInt(24) + 1;

        t = (t & ~(0xff << f)) | (b[i] << f);

        sb.append("t = ");
        sb.append(t);
        sb.append(";");
        sb.append("buf[");
        sb.append(i);
        sb.append("] = (byte) (t >>> ");
        sb.append(f);
        sb.append(");");
    }

    sb.append("return new String(buf);");
    sb.append("}}.toString())");

    return sb.toString();
}


android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "uk.co.thedistance.components.pocketseodemo"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "MOZSCAPE_ACCESS_KEY", "\"mozscape-6b2e08eff3\""
        buildConfigField "String", "MOZSCAPE_SECRET_KEY", toJavaCodeString("7027a20f523c0262cdeba1a39bfa0db1")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta4'
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta4'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.0.2'
    compile 'com.google.dagger:dagger:2.0.2'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    compile 'com.squareup.okhttp3:logging-interceptor:3.1.2'
    compile 'com.squareup.okhttp3:okhttp:3.1.2'
    compile 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0-beta4'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile project(':components')
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'org.apmem.tools:layouts:1.9@aar'
}
